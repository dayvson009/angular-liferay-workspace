plugins {
  id "de.richsource.gradle.plugins.typescript" version "1.8.0"
}

dependencies {
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "2.0.0"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "2.0"
	compileOnly group: "javax.servlet", name: "servlet-api", version: "2.5"
	compileOnly group: "jstl", name: "jstl", version: "1.2"
	compileOnly group: "org.osgi", name: "org.osgi.compendium", version: "5.0.0"
}

node {
	nodeVersion = "7.4.0"
}

def isWindows = org.gradle.internal.os.OperatingSystem.current().windows
def nodeExe = isWindows ? "node" : "build/node/bin/node"

compileTypeScript {
	compilerExecutable nodeExe + " node_modules/typescript/lib/tsc.js"
	dependsOn "npmInstall"
	projectFile = file("${projectDir}")
	source = "src/main/resources/META-INF/resources"
	outputDir = file("build/resources/main/META-INF/resources")
}

task copyAngularModules(type: Copy) {
	from ('node_modules/@angular') {
		into '@angular'
	}
	from ('node_modules/systemjs') {
		into 'systemjs'
	}
	from ('node_modules/rxjs') {
		into 'rxjs'
	}
	from ('node_modules/zone.js') {
		into 'zone.js'
	}
	from ('node_modules/core-js') {
		into 'core-js'
	}
	from ('node_modules/reflect-metadata') {
		into 'reflect-metadata'
	}
	from ('node_modules/angular-in-memory-web-api') {
		into 'angular-in-memory-web-api'
	}
	into 'build/resources/main/META-INF/resources/node_modules'
}

jar.dependsOn compileTypeScript, copyAngularModules
